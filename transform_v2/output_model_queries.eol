var arduinoTitle = "Arduino Uno (Rev3)";

var board = Part.all.selectOne(p | p.title == arduinoTitle).println();

var connections : Map<Connector, Connector>;

for (connector in board.connectors) {
  var sensor = bfsSensors(connector);
  if(sensor <> null) {
   connections.put(connector, sensor);
  }
}

for(k in connections.keySet()) {
  k.print();
  " -> ".print();
  connections.get(k).parent.println();
}

operation bfsSensors(start : Connector) : Connector {
  var queue = new Native("java.util.LinkedList");
  queue.add(start);
  var visited : Set<Connector>;
  
  while(not queue.isEmpty()) {
    var con = queue.removeFirst();
    if(not visited.includes(con)) {
      visited.add(con);
      
      if(con.parent.type == Type#Input or con.parent.type == Type#Output) {
        return con;
      }
      queue.add(con.connectedTo);
      
      if(not (con.parent.title == arduinoTitle)) {
        for(c in con.parent.connectors) {
          queue.add(c);
        }
      }
    }
  }
  
  return null;
}